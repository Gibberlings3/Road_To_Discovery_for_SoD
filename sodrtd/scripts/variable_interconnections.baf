////////////////////////////////////////////////////////////////////
/* "C#RtD_CaelarWantsPC" -> 2 */
/* 2 - Knows that Caelar has plans for the PC / wants the PC for [something]  */

/* 
IF 
3 - reasons / concludes / speculates that Caelar / Hephernaan want PC's blood for [open portal to Avernus] 
OR
4 - knows that Caelar / Hephernaan want PC's blood for [open portal to Avernus]
"C#RtD_KnowsPortalBlood" GT 2
THEN
2 - Knows that Caelar has plans for the PC / wants the PC for [something] 
"C#RtD_CaelarWantsPC" -> 2 
*/
IF
	GlobalLT("C#RtD_CaelarWantsPC","GLOBAL",2)
	GlobalGT("C#RtD_KnowsPortalBlood","GLOBAL",2)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_CaelarWantsPC","GLOBAL",2)
END

/* 
IF 
1 - knows that Caelar / Hephernaan want the PC's Bhaal child blood [for something]   
"C#RtD_WantBhaalBlood" GT 0 
THEN
2 - Knows that Caelar has plans for the PC / wants the PC for [something] 
"C#RtD_CaelarWantsPC" -> 2
*/
IF
	GlobalLT("C#RtD_CaelarWantsPC","GLOBAL",2)
	GlobalGT("C#RtD_WantBhaalBlood","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_CaelarWantsPC","GLOBAL",2)
END

/////////////////////////////////////////////////////////////////
/* "C#RtD_CaelarWantsPC" -> 1 */
/* 1 - assumes that Caelar wants to capture the PC [alive] */

/* 
IF
1 - PC discussed with someone/NPCs that Caelar's assassin probably wanted to kidnap PC in Palace (instead of killing) 
"C#RtD_CaelarKidnap"  GT 0 
THEN
1 - assumes that Caelar wants to capture the PC [alive] 
"C#RtD_CaelarWantsPC" -> 1 
*/
IF
	Global("C#RtD_CaelarWantsPC","GLOBAL",0)
	GlobalGT("C#RtD_CaelarKidnap","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_CaelarWantsPC","GLOBAL",1)
END


///////////////////////////////////////////////////////////////////////
/* "C#RtD_KnowsPortalBlood" -> 4
4 - knows that Caelar / Hephernaan want PC's blood for [open portal to Avernus]
*/

/*
"C#RtD_KnowsPortalBlood" -> 4
IF
"C#RtD_CaelarBhaalChild"
3 - knows that [rumors that Caelar is a child of Bhaal] is not true
AND
"C#RtD_KnowsPortalBlood"
2 - knows that blood of Bhaal child is needed/sufficient to open portal to Avernus under DC
THEN
"C#RtD_KnowsPortalBlood" -> 4
4 - knows that Caelar / Hephernaan want PC's blood for [open portal to Avernus] -- also for [C#RtD_WantBhaalBlood = 3]
*/

IF
	OR(2)
		Global("C#RtD_KnowsPortalBlood","GLOBAL",2)
		Global("C#RtD_KnowsPortalBlood","GLOBAL",3)
	Global("C#RtD_CaelarBhaalChild","GLOBAL",3)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_KnowsPortalBlood","GLOBAL",4)
END

/*
IF
"C#RtD_WantBhaalBlood" = 3
3 - knows that Caelar / Hephernaan want the PC's Bhaal child blood for opening the portal
THEN
"C#RtD_KnowsPortalBlood" -> 4
4 - knows that Caelar / Hephernaan want PC's blood for [open portal to Avernus]
*/

IF
	Global("C#RtD_WantBhaalBlood","GLOBAL",3)
	GlobalLT("C#RtD_KnowsPortalBlood","GLOBAL",4)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_KnowsPortalBlood","GLOBAL",4)
END

///////////////////////////////////////////////////////////////////////
/* "C#RtD_KnowsPortalBlood" -> 3
3 - reasons / concludes / specualtes that Caelar / Hephernaan want PC's blood for [open portal to Avernus] */

/* 
IF
2 - Knows that Caelar has plans for the PC / wants the PC for [something] 
"C#RtD_CaelarWantsPC" GT 1
AND
2 - knows that blood of Bhaal child is needed to open portal to Avernus 
"C#RtD_KnowsPortalBlood" = 2
THEN 
3 - reasons / concludes / speculates that Caelar / Hephernaan want PC's blood for [open portal to Avernus] 
"C#RtD_KnowsPortalBlood" -> 3
*/
IF
	Global("C#RtD_KnowsPortalBlood","GLOBAL",2)
	GlobalGT("C#RtD_CaelarWantsPC","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_KnowsPortalBlood","GLOBAL",3)
END

/* 
IF
1 - knows that Caelar / Hephernaan want the PC's Bhaal child blood [for something] 
"C#RtD_WantBhaalBlood" GT 0
AND
2 - knows that blood of Bhaal child is needed to open portal to Avernus
"C#RtD_KnowsPortalBlood" = 2
THEN 
3 - reasons / concludes / speculates that Caelar / Hephernaan want PC's blood for [open portal to Avernus]
"C#RtD_KnowsPortalBlood" -> 3
*/
IF
	Global("C#RtD_KnowsPortalBlood","GLOBAL",2)
	GlobalGT("C#RtD_WantBhaalBlood","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_KnowsPortalBlood","GLOBAL",3)
END

/*
"C#RtD_KnowsPortalBlood" -> 3
IF
"C#RtD_CaelarBhaalChild"
2 - reasons / concludes / speculates that [rumors that Caelar is a child of Bhaal] is not true
AND
"C#RtD_KnowsPortalBlood"
2 - knows that blood of Bhaal child is needed/sufficient to open portal to Avernus under DC
THEN
"C#RtD_KnowsPortalBlood" -> 3
3 - reasons / concludes / speculates that Caelar / Hephernaan want PC's blood for [open portal to Avernus] -- also for [C#RtD_WantBhaalBlood = 2]
*/
IF
	Global("C#RtD_KnowsPortalBlood","GLOBAL",2)
	Global("C#RtD_CaelarBhaalChild","GLOBAL",2)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_KnowsPortalBlood","GLOBAL",3)
END

/*
IF
"C#RtD_WantBhaalBlood" = 2
2 - reasons / concludes / speculates that Caelar / Hephernaan want the PC's Bhaal child blood for opening the portal
THEN
"C#RtD_KnowsPortalBlood" -> 3
3 - reasons / concludes / speculates that Caelar / Hephernaan want PC's blood for [open portal to Avernus] -- also for [C#RtD_WantBhaalBlood = 2]
*/
IF
	Global("C#RtD_WantBhaalBlood","GLOBAL",2)
	GlobalLT("C#RtD_KnowsPortalBlood","GLOBAL",3)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_KnowsPortalBlood","GLOBAL",3)
END

////////////////////////////////////////////////////////////////////////
/* "C#RtD_HephernaanFiend" -> 2 
2 - knows that Hephernaan [who is Caelar's advisor] is working for a fiend to open the portal */

/* 
IF
1 - knows Hephernaan by name and face and that he is Caelar's advisor 
"C#RtD_HephernaanIdentity" GT 0
AND
1 - knows that [a man being called] Hephernaan is working for a fiend to open the portal 
"C#RtD_HephernaanFiend" = 1
THEN
2 - knows that Hephernaan [who is Caelar's advisor] is working for a fiend to open the portal 
"C#RtD_HephernaanFiend" -> 2 
*/
IF
	Global("C#RtD_HephernaanFiend","GLOBAL",1)
	GlobalGT("C#RtD_HephernaanIdentity","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_HephernaanFiend","GLOBAL",2)
END

/* 
IF
1 - knows Hephernaan by name and face and that he is Caelar's advisor 
"C#RtD_HephernaanIdentity" GT 0
AND
1 - knows that [a man looking like] Hephernaan is working with Umbral/a fiend "C#RtD_HephernaanVisual" GT 0
THEN
2 - knows that Hephernaan [who is Caelar's advisor] is working for a fiend to open the portal 
"C#RtD_HephernaanFiend" -> 2 
*/
IF
	GlobalLT("C#RtD_HephernaanFiend","GLOBAL",2)
	GlobalGT("C#RtD_HephernaanVisual","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_HephernaanFiend","GLOBAL",2)
END


///////////////////////////////////////////////////////////////
/* "C#RtD_CaelarPlan" -> 5 
5 - speculates that Caelar wants to save her uncle Aun Argent from Avernus */

/*
IF
2 - knows that they want to free souls from Dragonspear War from Avernus
OR
3 - knows that they are planning on marching into Avernus
OR
4 - knows that they are planning on opening a portal at Dragonspear Castle to enter Avernus
"C#RtD_CaelarPlan" 2,3,4
AND
4 - knows that Aun took place for Caelar in Avernus
"C#RtD_KnowsAunArgent" = 4
THEN
5 - speculates that Caelar wants to save her uncle Aun Argent from Avernus
"C#RtD_CaelarPlan" -> 5 
*/
IF
	OR(3)
		Global("C#RtD_CaelarPlan","GLOBAL",2)
		Global("C#RtD_CaelarPlan","GLOBAL",3)
		Global("C#RtD_CaelarPlan","GLOBAL",4)
	Global("C#RtD_KnowsAunArgent","GLOBAL",4)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_CaelarPlan","GLOBAL",5)
END


///////////////////////////////////////////////////////////////
/* "C#RtD_CaelarPlan" -> 4 */
/* 4 - knows that they are planning on opening a portal at Dragonspear Castle to enter Avernus */

/* 
IF
2 - knows that they want to free souls from Dragonspear War from Avernus
OR
3 - knows that they are planning on marching into Avernus 
"C#RtD_CaelarPlan" 2 OR 3
AND
2 - knows that Hephernaan [who is Caelar's advisor] is working for a fiend to open the portal 
OR
3 - knows that Hephernaan [who is Caelar's advisor] is working for Belhifet 
"C#RtD_HephernaanFiend" GT 1
THEN
4 - knows that they are planning on opening a portal at Dragonspear Castle to enter Avernus 
"C#RtD_CaelarPlan" -> 4
*/
IF
	OR(2)
		Global("C#RtD_CaelarPlan","GLOBAL",2)
		Global("C#RtD_CaelarPlan","GLOBAL",3)
	GlobalGT("C#RtD_HephernaanFiend","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_CaelarPlan","GLOBAL",4)
END


/* 
IF
2 - knows that they want to free souls from Dragonspear War from Avernus
OR
3 - knows that they are planning on marching into Avernus 
"C#RtD_CaelarPlan" 2 OR 3
AND
3 - reasons / concludes / speculates that Caelar / Hephernaan want PC's blood for [open portal to Avernus] 
OR 4 
"C#RtD_KnowsPortalBlood" GT 2
THEN
4 - knows that they are planning on opening a portal at Dragonspear Castle to enter Avernus 
"C#RtD_CaelarPlan" -> 4
*/
/* deprecated - PC should realize sooner
IF
	OR(2)
		Global("C#RtD_CaelarPlan","GLOBAL",2)
		Global("C#RtD_CaelarPlan","GLOBAL",3)
	GlobalGT("C#RtD_KnowsPortalBlood","GLOBAL",2)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_CaelarPlan","GLOBAL",4)
END
*/

/*
IF
2 - knows that they want to free souls from Dragonspear War from Avernus
OR
3 - knows that they are planning on marching into Avernus 
"C#RtD_CaelarPlan" 2 OR 3
AND
1 - PC heard about a portal/rift in Dragonspear Castle
C#RtD_KnowsPortalBlood GT 0

THEN
C#RtD_CaelarPlan -> 4
4 - PC knows that they are planning on opening a portal at Dragonspear Castle to enter Avernus
*/
IF
	OR(2)
		Global("C#RtD_CaelarPlan","GLOBAL",2)
		Global("C#RtD_CaelarPlan","GLOBAL",3)
	GlobalGT("C#RtD_KnowsPortalBlood","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_CaelarPlan","GLOBAL",4)
END

////////////////////////////////////////////////////////////////////////
/* "C#RtD_HephernaanBetrayal" -> 2
2 - knows that Hephernaan [who is Caelar's advisor] is betraying Caelar */

/* 
IF
1 - knows Hephernaan by name and face and that he is Caelar's advisor 
"C#RtD_HephernaanIdentity" GT 0
AND
1 - know that [a man called] Hephernaan is betraying Caelar. 
"C#RtD_HephernaanBetrayal" = 1
THEN
2 - knows that Hephernaan [who is Caelar's advisor] is betraying Caelar 
"C#RtD_HephernaanBetrayal" -> 2
*/
IF
	Global("C#RtD_HephernaanBetrayal","GLOBAL",1)
	GlobalGT("C#RtD_HephernaanIdentity","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_HephernaanBetrayal","GLOBAL",2)
END

/* 
IF
1 - knows Hephernaan by name and face and that he is Caelar's advisor 
"C#RtD_HephernaanIdentity" GT 0
AND
1 - knows that [a man looking like] Hephernaan is working with Umbral/a fiend "C#RtD_HephernaanVisual" GT 0
THEN
2 - knows that Hephernaan [who is Caelar's advisor] is betraying Caelar 
"C#RtD_HephernaanBetrayal" -> 2
*/
IF
	GlobalLT("C#RtD_HephernaanBetrayal","GLOBAL",2)
	GlobalGT("C#RtD_HephernaanIdentity","GLOBAL",0)
	GlobalGT("C#RtD_HephernaanVisual","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_HephernaanBetrayal","GLOBAL",2)
END

/* 
IF
2 - knows that Hephernaan [who is Caelar's advisor] is working for a fiend to open the portal 
OR
3 - knows that Hephernaan [who is Caelar's advisor] is working for Belhifet 
"C#RtD_HephernaanFiend" GT 1
THEN
2 - knows that Hephernaan [who is Caelar's advisor] is betraying Caelar 
"C#RtD_HephernaanBetrayal" -> 2
*/
IF
	GlobalLT("C#RtD_HephernaanBetrayal","GLOBAL",2)
	GlobalGT("C#RtD_HephernaanFiend","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_HephernaanBetrayal","GLOBAL",2)
END

////////////////////////////////////////////////////////////////////////
/* "C#RtD_CaelarBetrayal" -> 1
1 - knows that Caelar is betrayed by someone in her crusade, that there is a deeper level to the crusade's purpose */

/* 
IF
2 - knows that Hephernaan [who is Caelar's advisor] is betraying Caelar 
"C#RtD_HephernaanBetrayal" GT 1
THEN 
1 - knows that Caelar is betrayed by someone in her crusade, that there is a deeper level to the crusade's purpose 
"C#RtD_CaelarBetrayal" -> 1
*/
IF
	Global("C#RtD_CaelarBetrayal","GLOBAL",0)
	GlobalGT("C#RtD_HephernaanBetrayal","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_CaelarBetrayal","GLOBAL",1)
END

/* 
IF
2 - knows that Hephernaan [who is Caelar's advisor] is working for a fiend to open the portal 
OR
3 - knows that Hephernaan [who is Caelar's advisor] is working for Belhifet 
"C#RtD_HephernaanFiend" GT 1
THEN
1 - knows that Caelar is betrayed by someone in her crusade, that there is a deeper level to the crusade's purpose 
"C#RtD_CaelarBetrayal" -> 1
*/
IF
	Global("C#RtD_CaelarBetrayal","GLOBAL",0)
	GlobalGT("C#RtD_HephernaanFiend","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_CaelarBetrayal","GLOBAL",1)
END


/////////////////////////////////////////////////////////////////
/* "C#RtD_CaelarBhaalChild" -> 3
3 - knows that [rumors that Caelar is a child of Bhaal] is not true */

/* 
IF 
1 - heard rumors that Caelar is a child of Bhaal
"C#RtD_CaelarBhaalChild" = 1
AND
/* 4 - knows that Caelar / Hephernaan want PC's blood for [open portal to Avernus] */
"C#RtD_KnowsPortalBlood" = 4
THEN
3 - knows that [rumors that Caelar is a child of Bhaal] is not true 
"C#RtD_CaelarBhaalChild" -> 3
*/
IF
	Global("C#RtD_CaelarBhaalChild","GLOBAL",1)
	Global("C#RtD_KnowsPortalBlood","GLOBAL",4)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_CaelarBhaalChild","GLOBAL",3)
END


/////////////////////////////////////////////////////////////////
/* "C#RtD_CaelarBhaalChild" -> 2
2 - knows that [rumors that Caelar is a child of Bhaal] is not true */

/* 
IF
/* 
1 - heard rumors that Caelar is a child of Bhaal
"C#RtD_CaelarBhaalChild" = 1
AND
1 - knows that Caelar / Hephernaan want the PC's Bhaal child blood 
"C#RtD_WantBhaalBlood" = 1
THEN
2 - reasons / concludes / speculates that [rumors that Caelar is a child of Bhaal] is not true */
"C#RtD_CaelarBhaalChild" -> 2
*/
IF
	Global("C#RtD_CaelarBhaalChild","GLOBAL",1)
	Global("C#RtD_WantBhaalBlood","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_CaelarBhaalChild","GLOBAL",2)
END

/* 
IF
1 - heard rumors that Caelar is a child of Bhaal 
"C#RtD_CaelarBhaalChild" = 1
AND
3 - reasons / concludes / speculates that Caelar / Hephernaan want PC's blood for [open portal to Avernus] 
"C#RtD_KnowsPortalBlood" = 3
THEN
2 - reasons / concludes / speculates that [rumors that Caelar is a child of Bhaal] is not true 
"C#RtD_CaelarBhaalChild" -> 2
*/
IF
	Global("C#RtD_CaelarBhaalChild","GLOBAL",1)
	Global("C#RtD_KnowsPortalBlood","GLOBAL",3)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_CaelarBhaalChild","GLOBAL",2)
END

/////////////////////////////////////////////////////////////////
/* 
"C#RtD_WantBhaalBlood" -> 3
3 - knows that Caelar / Hephernaan want the PC's Bhaal child blood for opening the portal
*/
/*
IF
"C#RtD_KnowsPortalBlood" = 4
4 - knows that Caelar / Hephernaan want PC's blood for [open portal to Avernus]
THEN
"C#RtD_WantBhaalBlood" -> 3
3 - knows that Caelar / Hephernaan want the PC's Bhaal child blood for opening the portal
*/
IF
	Global("C#RtD_KnowsPortalBlood","GLOBAL",4)
	GlobalLT("C#RtD_WantBhaalBlood","GLOBAL",3)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_WantBhaalBlood","GLOBAL",3)
END

/////////////////////////////////////////////////////////////////
/* 
"C#RtD_WantBhaalBlood" -> 2
2 - reasons / concludes / speculates that Caelar / Hephernaan want the PC's Bhaal child blood for opening the portal
*/
/*
IF
"C#RtD_KnowsPortalBlood" = 3
3 - reasons / concludes / speculates that Caelar / Hephernaan want PC's blood for [open portal to Avernus] 
THEN
"C#RtD_WantBhaalBlood" -> 2
2 - reasons / concludes / speculates that Caelar / Hephernaan want the PC's Bhaal child blood for opening the portal
*/
IF
	Global("C#RtD_KnowsPortalBlood","GLOBAL",3)
	GlobalLT("C#RtD_WantBhaalBlood","GLOBAL",2)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_WantBhaalBlood","GLOBAL",2)
END


/////////////////////////////////////////////////////////////////
/*
"C#RtD_HephernaanIdentity" -> 1
1 - knows Hephernaan by name and face and that he is Caelar's advisor 
*/
/* 
IF
2 - knows that Hephernaan [who is Caelar's advisor] is working for a fiend to open the portal 
"C#RtD_HephernaanFiend" GT 1
THEN
1 - knows Hephernaan by name and face and that he is Caelar's advisor 
"C#RtD_HephernaanIdentity" -> 1
*/
IF
	Global("C#RtD_HephernaanIdentity","GLOBAL",0)
	GlobalGT("C#RtD_HephernaanFiend","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_HephernaanIdentity","GLOBAL",1)
END
