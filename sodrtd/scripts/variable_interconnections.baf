////////////////////////////////////////////////////////////////////
/* "C#RtD_CaelarProtection" -> 4 */
/* 4 - PC knows that [the gods are protecting Caelar and them] is a lie */

/*
IF
3 - PC knows that [Caelar claims to be blessed by "the pantheon" / all gods /several gods] is a lie
"C#RtD_CaelarGodBless" = 3
THEN
4 - PC knows that [the gods are protecting Caelar and them] is a lie
"C#RtD_CaelarProtection" -> 4
*/

IF
	GlobalLT("C#RtD_CaelarProtection","GLOBAL",3)
	Global("C#RtD_CaelarGodBless","GLOBAL",4)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_CaelarProtection","GLOBAL",3)
END



////////////////////////////////////////////////////////////////////
/* "C#RtD_CaelarProtection" -> 2 */
/* 2 - PC knows that her followers believe that the gods are protecting Caelar and them */

/*
IF
2 - PC knows that Caelar claims to be blessed by "the pantheon" / all gods /several gods
"C#RtD_CaelarGodBless" = 2
THEN 
2 - PC knows that her followers believe that the gods are protecting Caelar and them
"C#RtD_CaelarProtection" -> 2
*/

IF
	GlobalLT("C#RtD_CaelarProtection","GLOBAL",2)
	Global("C#RtD_CaelarGodBless","GLOBAL",3)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_CaelarProtection","GLOBAL",2)
END



////////////////////////////////////////////////////////////////////
/* "C#RtD_CaelarGodBless" -> 3 */
/* 3 - PC knows that [Caelar claims to be blessed by "the pantheon" / all gods /several gods] is a lie */

/*
IF
4 - PC knows that [the gods are protecting Caelar and them] is a lie
"C#RtD_CaelarProtection" = 4
THEN
3 - PC knows that [Caelar claims to be blessed by "the pantheon" / all gods /several gods] is a lie
"C#RtD_CaelarGodBless" -> 3
*/
IF
	GlobalLT("C#RtD_CaelarGodBless","GLOBAL",3)
	Global("C#RtD_CaelarProtection","GLOBAL",4)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_CaelarGodBless","GLOBAL",3)
END

////////////////////////////////////////////////////////////////////
/* "C#RtD_CaelarGodBless" -> 2 */
/* 2 - PC knows that Caelar claims to be blessed by "the pantheon" / all gods /several gods */

/* 
IF
2 - PC knows that her followers believe that the gods are protecting Caelar and them
OR
3 - PC watched Hormorn's "blessing" in Bridgefort camp 
"C#RtD_CaelarProtection" = 2 OR 3
THEN
2 - PC knows that Caelar claims to be blessed by "the pantheon" / all gods /several gods
"C#RtD_CaelarGodBless" -> 2
*/

IF
	GlobalLT("C#RtD_CaelarGodBless","GLOBAL",2)
	OR(2)
		Global("C#RtD_CaelarProtection","GLOBAL",2)
		Global("C#RtD_CaelarProtection","GLOBAL",3)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_CaelarGodBless","GLOBAL",2)
END




////////////////////////////////////////////////////////////////////
/* "C#RtD_CaelarWantsPC" -> 2 */
/* 2 - Knows that Caelar has plans for the PC / wants the PC for [something]  */

/* 
IF 
3 - reasons / concludes / speculates that Caelar / Hephernaan want PC's blood for [open portal to Avernus] 
OR
4 - knows that Caelar / Hephernaan want PC's blood for [open portal to Avernus]
"C#RtD_KnowsPortalBlood" GT 2
THEN
2 - Knows that Caelar has plans for the PC / wants the PC for [something] 
"C#RtD_CaelarWantsPC" -> 2 
*/
IF
	GlobalLT("C#RtD_CaelarWantsPC","GLOBAL",2)
	GlobalGT("C#RtD_KnowsPortalBlood","GLOBAL",2)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_CaelarWantsPC","GLOBAL",2)
END

/* 
IF 
1 - knows that Caelar / Hephernaan want the PC's Bhaal child blood [for something]   
"C#RtD_WantBhaalBlood" GT 0 
THEN
2 - Knows that Caelar has plans for the PC / wants the PC for [something] 
"C#RtD_CaelarWantsPC" -> 2
*/
IF
	GlobalLT("C#RtD_CaelarWantsPC","GLOBAL",2)
	GlobalGT("C#RtD_WantBhaalBlood","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_CaelarWantsPC","GLOBAL",2)
END


/////////////////////////////////////////////////////////////////
/* "C#RtD_CaelarWantsPC" -> 1 */
/* 1 - assumes that Caelar wants to capture the PC [alive] */

/* 
IF
1 - PC discussed with someone/NPCs that Caelar's assassin probably wanted to kidnap PC in Palace (instead of killing) 
"C#RtD_CaelarKidnap"  GT 0 
THEN
1 - assumes that Caelar wants to capture the PC [alive] 
"C#RtD_CaelarWantsPC" -> 1 
*/
IF
	Global("C#RtD_CaelarWantsPC","GLOBAL",0)
	GlobalGT("C#RtD_CaelarKidnap","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_CaelarWantsPC","GLOBAL",1)
END


///////////////////////////////////////////////////////////////////////
/* "C#RtD_KnowsPortalBlood" -> 4
4 - knows that Caelar / Hephernaan want PC's blood for [open portal to Avernus]
*/

/*
IF
"C#RtD_WantBhaalBlood" = 3
3 - knows that Caelar / Hephernaan want the PC's Bhaal child blood for opening the portal
THEN
"C#RtD_KnowsPortalBlood" -> 4
4 - knows that Caelar / Hephernaan want PC's blood for [open portal to Avernus]
*/

IF
	Global("C#RtD_WantBhaalBlood","GLOBAL",3)
	GlobalLT("C#RtD_KnowsPortalBlood","GLOBAL",4)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_KnowsPortalBlood","GLOBAL",4)
END


///////////////////////////////////////////////////////////////////////
/* "C#RtD_KnowsPortalBlood" -> 3
3 - reasons / concludes / specualtes that Caelar / Hephernaan want PC's blood for [open portal to Avernus] */


/*
IF
"C#RtD_WantBhaalBlood" = 2
2 - reasons / concludes / speculates that Caelar / Hephernaan want the PC's Bhaal child blood for opening the portal
THEN
"C#RtD_KnowsPortalBlood" -> 3
3 - reasons / concludes / speculates that Caelar / Hephernaan want PC's blood for [open portal to Avernus] -- also for [C#RtD_WantBhaalBlood = 2]
*/
IF
	Global("C#RtD_WantBhaalBlood","GLOBAL",2)
	GlobalLT("C#RtD_KnowsPortalBlood","GLOBAL",3)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_KnowsPortalBlood","GLOBAL",3)
END


////////////////////////////////////////////////////////////////////////
/* "C#RtD_HephernaanFiend" -> 2 
2 - knows that Hephernaan [who is Caelar's advisor] is working for a fiend to open the portal */

/* for the rare case that the PC already told the officers that Hephernaan is betraying Caelar but witnesses the pool scene afterwards */
IF
	GlobalLT("C#RtD_HephernaanFiend","GLOBAL",2)
	Global("C#RtD_HephernaanVisual","GLOBAL",1)
	Global("C#RtD_HephernaanIdentity","GLOBAL",1)
	Global("C#RtD_HephernaanBetrayal","GLOBAL",2)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_HephernaanFiend","GLOBAL",2)
END

////////////////////////////////////////////////////////////////////////
/* "C#RtD_HephernaanBetrayal" -> 2
2 - knows that Hephernaan [who is Caelar's advisor] is betraying Caelar */

/* 
IF
2 - knows that Hephernaan [who is Caelar's advisor] is working for a fiend to open the portal 
OR
3 - knows that Hephernaan [who is Caelar's advisor] is working for Belhifet 
"C#RtD_HephernaanFiend" GT 1
THEN
2 - knows that Hephernaan [who is Caelar's advisor] is betraying Caelar 
"C#RtD_HephernaanBetrayal" -> 2
*/
IF
	GlobalLT("C#RtD_HephernaanBetrayal","GLOBAL",2)
	GlobalGT("C#RtD_HephernaanFiend","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_HephernaanBetrayal","GLOBAL",2)
END

/* 
IF
2 - knows that Hephernaan [who is Caelar's advisor] is working for a fiend to open the portal 
OR
3 - knows that Hephernaan [who is Caelar's advisor] is working for Belhifet 
"C#RtD_HephernaanFiend" GT 1
THEN
1 - knows that Caelar is betrayed by someone in her crusade, that there is a deeper level to the crusade's purpose 
"C#RtD_CaelarBetrayal" -> 1
*/
IF
	Global("C#RtD_CaelarBetrayal","GLOBAL",0)
	GlobalGT("C#RtD_HephernaanFiend","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_CaelarBetrayal","GLOBAL",1)
END


///////////////////////////////////////////////////////////////
/* "C#RtD_CaelarPlan" -> 5 
5 - speculates that Caelar wants to save her uncle Aun Argent from Avernus */

/* no automated interconnection; all handled via dialogue */


///////////////////////////////////////////////////////////////
/* "C#RtD_CaelarPlan" -> 4 */
/* 4 - knows that they are planning on opening a portal at Dragonspear Castle to enter Avernus */

/* no automated interconnection; all handled via dialogue */


////////////////////////////////////////////////////////////////////////
/* "C#RtD_HephernaanBetrayal" -> 2
2 - knows that Hephernaan [who is Caelar's advisor] is betraying Caelar */

/* no automated interconnection; all handled via dialogue */


////////////////////////////////////////////////////////////////////////
/* "C#RtD_CaelarBetrayal" -> 1
1 - knows that Caelar is betrayed by someone in her crusade, that there is a deeper level to the crusade's purpose */

/* 
IF
2 - knows that Hephernaan [who is Caelar's advisor] is betraying Caelar 
"C#RtD_HephernaanBetrayal" GT 1
THEN 
1 - knows that Caelar is betrayed by someone in her crusade, that there is a deeper level to the crusade's purpose 
"C#RtD_CaelarBetrayal" -> 1
*/
IF
	Global("C#RtD_CaelarBetrayal","GLOBAL",0)
	GlobalGT("C#RtD_HephernaanBetrayal","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_CaelarBetrayal","GLOBAL",1)
END


/////////////////////////////////////////////////////////////////
/* "C#RtD_CaelarBhaalChild" -> 2
2 - knows that [rumors that Caelar is a child of Bhaal] is not true */

/* no automated interconnection; all handled via dialogue */


/////////////////////////////////////////////////////////////////
/* 
"C#RtD_WantBhaalBlood" -> 3
3 - knows that Caelar / Hephernaan want the PC's Bhaal child blood for opening the portal
*/
/*
IF
"C#RtD_KnowsPortalBlood" = 4
4 - knows that Caelar / Hephernaan want PC's blood for [open portal to Avernus]
THEN
"C#RtD_WantBhaalBlood" -> 3
3 - knows that Caelar / Hephernaan want the PC's Bhaal child blood for opening the portal
*/
IF
	Global("C#RtD_KnowsPortalBlood","GLOBAL",4)
	GlobalLT("C#RtD_WantBhaalBlood","GLOBAL",3)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_WantBhaalBlood","GLOBAL",3)
END

/////////////////////////////////////////////////////////////////
/* 
"C#RtD_WantBhaalBlood" -> 2
2 - reasons / concludes / speculates that Caelar / Hephernaan want the PC's Bhaal child blood for opening the portal
*/
/*
IF
"C#RtD_KnowsPortalBlood" = 3
3 - reasons / concludes / speculates that Caelar / Hephernaan want PC's blood for [open portal to Avernus] 
THEN
"C#RtD_WantBhaalBlood" -> 2
2 - reasons / concludes / speculates that Caelar / Hephernaan want the PC's Bhaal child blood for opening the portal
*/
IF
	Global("C#RtD_KnowsPortalBlood","GLOBAL",3)
	GlobalLT("C#RtD_WantBhaalBlood","GLOBAL",2)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_WantBhaalBlood","GLOBAL",2)
END


/////////////////////////////////////////////////////////////////
/*
"C#RtD_HephernaanIdentity" -> 1
1 - knows Hephernaan by name and face and that he is Caelar's advisor 
*/

/* this is a fail safe, just in case C#RtD_HephernaanIdentity wasn't set already when encountering Hephernaan as a minion for Belhifet. "C#RtD_HephernaanFiend" will only be set at "2" if the PC knows that Hephernaan is Caelar's advisor, i.e. the revelation of his betrayal happened with her etc. */
/* 
IF
2 - knows that Hephernaan [who is Caelar's advisor] is working for a fiend to open the portal 
"C#RtD_HephernaanFiend" GT 1
THEN
1 - knows Hephernaan by name and face and that he is Caelar's advisor 
"C#RtD_HephernaanIdentity" -> 1
*/
IF
	Global("C#RtD_HephernaanIdentity","GLOBAL",0)
	GlobalGT("C#RtD_HephernaanFiend","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("C#RtD_HephernaanIdentity","GLOBAL",1)
END
